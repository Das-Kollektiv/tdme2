function: checkPrivateScopeReference(&$var)
	console.printLine("\tcheckPrivateScopeReference(&$var)")
	$var.wheelCount = 123
end

function: checkPrivateScopeNonReference($var)
	console.printLine("\tcheckPrivateScopeNonReference($var)")
	$var.wheelCount = 456
end

# main
function: main()
	console.printLine("------------")
	console.printLine("class-test")
	console.printLine("------------")
	console.printLine()
	#
	$car = {
		# member variables
		-wheelCount: 4,
		-color: "blue",
		-horsePower: 75,
		# member methods
		setWheelCount: ($wheelCount) -> 
			{
				$this.wheelCount = $wheelCount
			},
		setColor: ($color) ->
			{
				$this.color = $color
			},
		setHorsePower: ($horsePower) -> 
			{
				$this.horsePower = $horsePower
			},
		showMeWhatYouGot: () ->
			{
				console.printLine(
					"This amazing car has ", 
					$this.wheelCount,
					" wheels, is colored ", 
					$this.color,
					" with super nice ", 
					$this.horsePower,
					" horses in it"
				)
			},
		getProperties: (&$wheelCount, &$color, &$horsePower) -> 
			{
				$wheelCount = $this.wheelCount
				$color = $this.color
				$horsePower = $this.horsePower
			}
		checkEngine: () -> {
			$engine = {
				-start: () -> {
					console.printLine("\tstarted engine")
				},
				-stop: () -> {
					console.printLine("\tstopped engine")
				}
				check: () -> {
					console.printLine("checking engine")
					$this->start()
					$this->stop()
					console.printLine("\tdone")
				},
			}
			$engine->check()
		},
		testPrivacy: () -> {
			console.printLine("testPrivacy()")
			console.printLine("\t" + "$this.wheelCount = " + $this.wheelCount)
			checkPrivateScopeNonReference($this)
			checkPrivateScopeReference($this)
			$this->testPrivateFunction()
		},
		-testPrivateFunction: () -> {
			console.printLine("\ttestPrivateFunction(): Hello World")
		},
		"-quotedKeytest": -1
	}
	#
	console.dump($car)
	# arrr, lets see what kind of car we got
	$car->showMeWhatYouGot()
	# i want it in red with 3 wheels and 25 mighty horse power
	$car->setColor("red")
	$car->setWheelCount(3)
	$car->setHorsePower(25)
	# arrr, lets see what kind of car we got now!
	$car->showMeWhatYouGot()
	#
	$wheelCount = null
	$color = null
	$horsePower = null
	$car->getProperties($wheelCount, $color, $horsePower)
	console.printLine(
		"Car properties, wheels: ", 
		$wheelCount,
		", color: ", 
		$color,
		", horse power: ", 
		$horsePower
	)
	# let me access some member thats private
	console.printLine(
		"Wheel count: ", 
		$car.wheelCount
	)
	# check engine
	$car->checkEngine()
	$car->testPrivacy()
	$car->testPrivateFunction()
end
