# initialize
on: initialize
	console.printLine("----------------------------")
	console.printLine("functions-test: Initialize")
	console.printLine("----------------------------")
	console.printLine()
end

# user script function of recursive factorial computation 
function: factorial($value)
	if ($value == 0) 
		return(1)
	end
	return($value * factorial($value - 1))
end 

# user script function to just display some hello world
function: helloWorldFunction()
	console.printLine("helloWorldFunction(): Hello world")
end

# user script function to test assign back in user functions
function: assignTest($a, &$b, &$c)
	$a = "a"
	$b = "b"
	$c = "c"
end

# user script function to test assign back in user functions
function: globalVariableTest()
	console.printLine("globalVariableTest(): $GLOBAL.globalTest = " + $GLOBAL.globalTest)
	$GLOBAL.globalTest = "Been there, done that, got the t-shirt"
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	console.printLine("-------------------------")
	console.printLine("functions-test: Nothing")
	console.printLine("-------------------------")
	helloWorldFunction()
	console.printLine("factorial(5) = " + factorial(5))
	$a = "0"
	$b = "1"
	$c = "2"
	console.printLine("assignTest(): pre: $a = " + $a + ", $b = " + $b + ", $c = " + $c)
	assignTest($a, $b, $c)
	console.printLine("assignTest(): post: $a = " + $a + ", $b = " + $b + ", $c = " + $c)
	$globalTest = "Global Test Variable"
	console.printLine("globalVariableTest(): pre: $globalTest = " + $globalTest)
	globalVariableTest()
	console.printLine("globalVariableTest(): post: $globalTest = " + $globalTest)
	script.stop()
end

# an error has occurred
on: error
	console.printLine("----------------------")
	console.printLine("functions-test: Error")
	console.printLine("----------------------")
	console.printLine("An error occurred")
	script.wait(1000)
end