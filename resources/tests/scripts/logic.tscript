# update engine
function: updateEngine()
	#console.log("-----------------------------")
	#console.log("updateEngine(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()

	# camera
	engine.camera.setLookAt($GLOBAL.transform->getTranslation())
	engine.camera.setLookFrom($GLOBAL.transform->getTranslation() + Vector3(0.0, 10.0, 10.0))
	engine.camera.setUpVector(engine.camera.computeUpVector(engine.camera.getLookFrom(), engine.camera.getLookAt())) 

	# update object from physics
	engine.entity.setTransform(logic.getId(), $GLOBAL.transform)

	# if no path finding
	if ($GLOBAL.pathFinding.pathIdx == -1)
		# update velocity based on arrow keys
		$GLOBAL.velocity = Vector3(0.0, 0.0, 0.0)
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_LEFT()) == true)
			$GLOBAL.velocity = $GLOBAL.velocity + Vector3(-1.0, 0.0, 0.0)
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_RIGHT()) == true)
			$GLOBAL.velocity = $GLOBAL.velocity + Vector3(1.0, 0.0, 0.0)
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_UP()) == true)
			$GLOBAL.velocity = $GLOBAL.velocity + Vector3(0.0, 0.0, -1.0)
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_DOWN()) == true)
			$GLOBAL.velocity = $GLOBAL.velocity + Vector3(0.0, 0.0, 1.0)
		end
	else
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_LEFT()) == true)
			$GLOBAL.pathFinding.pathIdx = -1
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_RIGHT()) == true)
			$GLOBAL.pathFinding.pathIdx = -1
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_UP()) == true)
			$GLOBAL.pathFinding.pathIdx = -1
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_DOWN()) == true)
			$GLOBAL.pathFinding.pathIdx = -1
		end
	end

	# switch animation
	$currentAnimation = engine.entity.getAnimation(logic.getId())
	if ($GLOBAL.velocity->computeLengthSquared() > math.EPSILON())
		# update velocity
		$GLOBAL.velocity = $GLOBAL.velocity->normalize() * $GLOBAL.speed
		#
		if ($currentAnimation != "walk") 
			engine.entity.setAnimation(logic.getId(), "walk")
		end
	else
		if ($currentAnimation != "idle") 
			engine.entity.setAnimation(logic.getId(), "idle")
		end
	end
	
	# mouse
	if (input.mouse.hasMoved() == true)
		$GLOBAL.mouseX = input.mouse.getXUnscaled()
		$GLOBAL.mouseY = input.mouse.getYUnscaled()
	end
	if (input.mouse.isButtonDown(input.mouse.BUTTON_LEFT()) == true)
		if ($GLOBAL.mouseX != -1 && $GLOBAL.mouseY != -1)
			if ($GLOBAL.entityId != "")
				engine.entity.setEffectColorAdd($GLOBAL.entityId, Vector4(0.0, 0.0, 0.0, 0.0))
			end
			$GLOBAL.entityId = engine.getEntityIdByMousePosition($GLOBAL.mouseX, $GLOBAL.mouseY)
			if ($GLOBAL.entityId != "")
				engine.entity.setEffectColorAdd($GLOBAL.entityId, Vector4(1.0, 0.0, 0.0, 0.0))
			end
			$clickWorldPosition = engine.computeWorldCoordinateByMousePosition($GLOBAL.mouseX, $GLOBAL.mouseY)
			$GLOBAL.pathFinding.enabled = true
			$GLOBAL.pathFinding.start = $GLOBAL.transform->getTranslation()
			$GLOBAL.pathFinding.end = $clickWorldPosition
			console.log("Clicked@worldposition: ", $clickWorldPosition)
		end
	end
end

# update logic
function: updateLogic()
	#console.log("-----------------------------")
	#console.log("updateLogic(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
	# path finding
	if ($GLOBAL.pathFinding.pathIdx >= 0)
		$GLOBAL.velocity = getPathFindingMovementDirection() * $GLOBAL.speed 
		if ($GLOBAL.velocity->computeLengthSquared() < math.square(math.EPSILON()))
			++$GLOBAL.pathFinding.pathIdx
			if ($GLOBAL.pathFinding.pathIdx >= $GLOBAL.pathFinding.path->length())
				$GLOBAL.pathFinding.pathIdx = -1
			end
			$GLOBAL.velocity = getPathFindingMovementDirection() * $GLOBAL.speed 
		end
	end
	# pass velocity to physics object
	world.body.setLinearVelocity(logic.getId(), $GLOBAL.velocity)
	# read back transform from physics object
	$GLOBAL.transform->setTranslation(world.body.getTransform(logic.getId())->getTranslation())
	# apply rotation angle and interpolate rotation
	if (math.abs(math.absmod($GLOBAL.rotation, 360.0) - math.absmod($GLOBAL.rotationLast, 360.0)) > math.EPSILON())
		$GLOBAL.rotationTimePassed = 0.0
	end
	$currentAngle = $GLOBAL.transform->getRotationAngle(1)
	$targetAngle = $GLOBAL.rotation
	$interpolatedAngle = 0.0
	if (Transform::interpolateRotation($currentAngle, $targetAngle, $GLOBAL.rotationTimePassed, 360.0, $interpolatedAngle) == true)
		$GLOBAL.rotationTimePassed = 0.0
	else
		$GLOBAL.rotationTimePassed = $GLOBAL.rotationTimePassed + engine.timing.getDeltaTimeSeconds()
	end
	$GLOBAL.transform->setRotationAngle(1, $interpolatedAngle)
	# path finding
	if ($GLOBAL.pathFinding.enabled == true)
		$GLOBAL.pathFinding.path = []
		$state = pathfinding.findPath(logic.getId(), $GLOBAL.pathFinding.start, $GLOBAL.pathFinding.end, $GLOBAL.pathFinding.path)
		if ($state == pathfinding.STATE_PATHFINDING_SUCCESS())
			$GLOBAL.pathFinding.enabled = false
			console.log("Pathfinding: Success: ", $GLOBAL.pathFinding.path)
			$GLOBAL.pathFinding.pathIdx = 0
		elseif ($state == pathfinding.STATE_PATHFINDING_FAILED())
			$GLOBAL.pathFinding.enabled = false
			console.log("Pathfinding: Fail: ")
		end
	end
	#
	$GLOBAL.rotationLast = $GLOBAL.rotation
	# rotation
	if (math.abs($GLOBAL.velocity->getX() - 0.0) > math.EPSILON())
		if ($GLOBAL.velocity->getX() > math.EPSILON())
			$GLOBAL.rotation = 90.0
		else
			$GLOBAL.rotation = 270.0
		end
	elseif (math.abs($GLOBAL.velocity->getZ() - 0.0) > math.EPSILON())
		if ($GLOBAL.velocity->getZ() > math.EPSILON())
			$GLOBAL.rotation = 0.0
		else
			$GLOBAL.rotation = 180.0
		end
	end
end

# logic added
function: onLogicAdded()
	#console.log("-----------------------------")
	#console.log("onLogicAdded(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
	#
	$GLOBAL.transform = engine.entity.getTransform(logic.getId())
	$GLOBAL.velocity = Vector3(0.0, 0.0, 0.0)
	$GLOBAL.speed = 4.0
	$GLOBAL.rotation = 180.0
	$GLOBAL.rotationLast = 180.0
	$GLOBAL.mouseX = -1
	$GLOBAL.mouseY = -1
	$GLOBAL.entityId = ""
	$GLOBAL.rotationTimePassed = 0.0
	$GLOBAL.pathFinding = {
		enabled: false,
		start: Vector3(0.0, 0.0, 0.0),
		end: Vector3(0.0, 0.0, 0.0),
		path: [],
		pathIdx: -1
	}
	console.log($GLOBAL.pathFinding)
	
end

# logics processed
function: onLogicsProcessed()
	#console.log("-----------------------------")
	#console.log("onLogicsProcessed(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
end

on: initialize
	console.log("-----------------------------")
	console.log("on: initialize: " + logic.getId())
	console.log("-----------------------------")
	console.log()
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	#console.log("-----------------------------")
	#console.log("on: nothing: " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
end

# an error has occurred
on: error
	console.log("An error occurred")
	script.stop()
end

function: getPathFindingMovementDirection()
	if ($GLOBAL.pathFinding.pathIdx == -1 || $GLOBAL.pathFinding.pathIdx >= $GLOBAL.pathFinding.path->length())
		return(Vector3(0.0, 0.0, 0.0))
	end
	$xDirection = 0.0
	$zDirection = 0.0
	$translation =  $GLOBAL.transform->getTranslation()
	if (math.abs($translation->getX() - $GLOBAL.pathFinding.path[$GLOBAL.pathFinding.pathIdx]->getX()) < 0.1)
		$xDirection = 0.0
	elseif ($translation->getX() < $GLOBAL.pathFinding.path[$GLOBAL.pathFinding.pathIdx]->getX())
		$xDirection = 1.0
	elseif ($translation->getX() > $GLOBAL.pathFinding.path[$GLOBAL.pathFinding.pathIdx]->getX())
		$xDirection = -1.0
	end
	if (math.abs($xDirection - 0.0) > math.EPSILON())
		return(Vector3($xDirection, 0.0, $zDirection))
	end
	if (math.abs($translation->getZ() - $GLOBAL.pathFinding.path[$GLOBAL.pathFinding.pathIdx]->getZ()) < 0.1)
		$zDirection = 0.0
	elseif ($translation->getZ() < $GLOBAL.pathFinding.path[$GLOBAL.pathFinding.pathIdx]->getZ())
		$zDirection = 1.0
	elseif ($translation->getZ() > $GLOBAL.pathFinding.path[$GLOBAL.pathFinding.pathIdx]->getZ())
		$zDirection = -1.0
	end
	return(Vector3($xDirection, 0.0, $zDirection))
end
