# update engine
function: updateEngine()
	#console.log("-----------------------------")
	#console.log("updateEngine(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()

	# camera
	engine.camera.setLookAt(transform.getTranslation($GLOBAL.transform))
	engine.camera.setLookFrom(transform.getTranslation($GLOBAL.transform) + vec3(0.0, 10.0, 10.0))
	engine.camera.setUpVector(engine.camera.computeUpVector(engine.camera.getLookFrom(), engine.camera.getLookAt())) 

	# update object from physics
	engine.entity.setTransform(logic.getId(), $GLOBAL.transform)

	# if no path finding
	if ($GLOBAL.pathIdx == -1)
		# update velocity based on arrow keys
		$GLOBAL.velocity = vec3(0.0, 0.0, 0.0)
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_LEFT()) == true)
			$GLOBAL.velocity = $GLOBAL.velocity + vec3(-1.0, 0.0, 0.0)
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_RIGHT()) == true)
			$GLOBAL.velocity = $GLOBAL.velocity + vec3(1.0, 0.0, 0.0)
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_UP()) == true)
			$GLOBAL.velocity = $GLOBAL.velocity + vec3(0.0, 0.0, -1.0)
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_DOWN()) == true)
			$GLOBAL.velocity = $GLOBAL.velocity + vec3(0.0, 0.0, 1.0)
		end
	else
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_LEFT()) == true)
			$GLOBAL.pathIdx = -1
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_RIGHT()) == true)
			$GLOBAL.pathIdx = -1
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_UP()) == true)
			$GLOBAL.pathIdx = -1
		end
		if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_DOWN()) == true)
			$GLOBAL.pathIdx = -1
		end
	end

	# switch animation
	$currentAnimation = engine.entity.getAnimation(logic.getId())
	if (vec3.computeLengthSquared($GLOBAL.velocity) > math.EPSILON())
		# update velocity
		$GLOBAL.velocity = vec3.normalize($GLOBAL.velocity)
		#	with speed
		$GLOBAL.velocity = $GLOBAL.velocity * $GLOBAL.speed
		#
		if ($currentAnimation != "walk") 
			engine.entity.setAnimation(logic.getId(), "walk")
		end
	else
		if ($currentAnimation != "idle") 
			engine.entity.setAnimation(logic.getId(), "idle")
		end
	end
	
	# mouse
	if (input.mouse.hasMoved() == true)
		$GLOBAL.mouseX = input.mouse.getXUnscaled()
		$GLOBAL.mouseY = input.mouse.getYUnscaled()
	end
	if (input.mouse.isButtonDown(input.mouse.BUTTON_LEFT()) == true)
		if ($GLOBAL.mouseX != -1 && $GLOBAL.mouseY != -1)
			if ($GLOBAL.entityId != "")
				engine.entity.setEffectColorAdd($GLOBAL.entityId, vec4(0.0, 0.0, 0.0, 0.0))
			end
			$GLOBAL.entityId = engine.getEntityIdByMousePosition($GLOBAL.mouseX, $GLOBAL.mouseY)
			if ($GLOBAL.entityId != "")
				engine.entity.setEffectColorAdd($GLOBAL.entityId, vec4(1.0, 0.0, 0.0, 0.0))
			end
			$clickWorldPosition = engine.computeWorldCoordinateByMousePosition($GLOBAL.mouseX, $GLOBAL.mouseY)
			$GLOBAL.pathFinding = true
			$GLOBAL.pathFindingStart = transform.getTranslation($GLOBAL.transform)
			$GLOBAL.pathFindingEnd = $clickWorldPosition
			console.log("Clicked@worldposition: ", $clickWorldPosition)
		end
	end
end

# update logic
function: updateLogic()
	#console.log("-----------------------------")
	#console.log("updateLogic(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
	# path finding
	if ($GLOBAL.pathIdx >= 0)
		$GLOBAL.velocity = getPathFindingMovementDirection() * $GLOBAL.speed 
		if (vec3.computeLengthSquared($GLOBAL.velocity) < math.square(math.EPSILON()))
			$GLOBAL.pathIdx = $GLOBAL.pathIdx + 1
			if ($GLOBAL.pathIdx >= array.length($GLOBAL.path))
				$GLOBAL.pathIdx = -1
			end
			$GLOBAL.velocity = getPathFindingMovementDirection() * $GLOBAL.speed 
		end
	end
	# pass velocity to physics object
	world.body.setLinearVelocity(logic.getId(), $GLOBAL.velocity)
	# read back transform from physics object
	transform.setTranslation($GLOBAL.transform, transform.getTranslation(world.body.getTransform(logic.getId())))
	# apply rotation angle and interpolate rotation
	if (math.abs(math.absmod($GLOBAL.rotation, 360.0) - math.absmod($GLOBAL.rotationLast, 360.0)) > math.EPSILON())
		$GLOBAL.rotationTimePassed = 0.0
	end
	$currentAngle = transform.getRotationAngle($GLOBAL.transform, 1)
	$targetAngle = $GLOBAL.rotation
	$interpolatedAngle = 0.0
	if (transform.interpolateRotation($currentAngle, $targetAngle, $GLOBAL.rotationTimePassed, 360.0, $interpolatedAngle) == true)
		$GLOBAL.rotationTimePassed = 0.0
	else
		$GLOBAL.rotationTimePassed = $GLOBAL.rotationTimePassed + engine.timing.getDeltaTimeSeconds()
	end
	transform.setRotationAngle($GLOBAL.transform, 1, $interpolatedAngle)
	# path finding
	if ($GLOBAL.pathFinding == true)
		$GLOBAL.path = array()
		$state = pathfinding.findPath(logic.getId(), $GLOBAL.pathFindingStart, $GLOBAL.pathFindingEnd, $GLOBAL.path)
		if ($state == pathfinding.STATE_PATHFINDING_SUCCESS())
			$GLOBAL.pathFinding = false
			console.log("Pathfinding: Success: ", $GLOBAL.path)
			$GLOBAL.pathIdx = 0
		elseif ($state == pathfinding.STATE_PATHFINDING_FAILED())
			$GLOBAL.pathFinding = false
			console.log("Pathfinding: Fail: ")
		end
	end
	#
	$GLOBAL.rotationLast = $GLOBAL.rotation
	# rotation
	if (math.abs(vec3.getX($GLOBAL.velocity) - 0.0) > math.EPSILON())
		if (vec3.getX($GLOBAL.velocity) > math.EPSILON())
			$GLOBAL.rotation = 90.0
		else
			$GLOBAL.rotation = 270.0
		end
	elseif (math.abs(vec3.getZ($GLOBAL.velocity) - 0.0) > math.EPSILON())
		if (vec3.getZ($GLOBAL.velocity) > math.EPSILON())
			$GLOBAL.rotation = 0.0
		else
			$GLOBAL.rotation = 180.0
		end
	end
end

# logic added
function: onLogicAdded()
	#console.log("-----------------------------")
	#console.log("onLogicAdded(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
	# initializing
	$GLOBAL.transform = engine.entity.getTransform(logic.getId())
	$GLOBAL.velocity = vec3(0.0, 0.0, 0.0)
	$GLOBAL.speed = 4.0
	$GLOBAL.rotation = 180.0
	$GLOBAL.rotationLast = 180.0
	$GLOBAL.mouseX = -1
	$GLOBAL.mouseY = -1
	$GLOBAL.entityId = ""
	$GLOBAL.pathFinding = false
	$GLOBAL.pathFindingStart = vec3(0.0, 0.0, 0.0)
	$GLOBAL.pathFindingEnd = vec3(0.0, 0.0, 0.0)
	$GLOBAL.path = array()
	$GLOBAL.pathIdx = -1
	$GLOBAL.rotationTimePassed = 0.0
end

# logics processed
function: onLogicsProcessed()
	#console.log("-----------------------------")
	#console.log("onLogicsProcessed(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
end

# initialize
on: initialize
	# this is not yet called
	#console.log("-----------------------------")
	#console.log("initialize: " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	#console.log("-----------------------------")
	#console.log("Nothing: " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
end

# an error has occurred
on: error
	console.log("An error occurred")
	script.stop()
end

function: getPathFindingMovementDirection()
	if ($GLOBAL.pathIdx == -1 || $GLOBAL.pathIdx >= array.length($GLOBAL.path))
		return(vec3(0.0, 0.0, 0.0))
	end
	$xDirection = 0.0
	$zDirection = 0.0
	$translation =  transform.getTranslation($GLOBAL.transform)
	if (math.abs(vec3.getX($translation) - vec3.getX($GLOBAL.path[$GLOBAL.pathIdx])) < 0.1)
		$xDirection = 0.0
	elseif (vec3.getX($translation) < vec3.getX($GLOBAL.path[$GLOBAL.pathIdx]))
		$xDirection = 1.0
	elseif (vec3.getX($translation) > vec3.getX($GLOBAL.path[$GLOBAL.pathIdx]))
		$xDirection = -1.0
	end
	if (math.abs($xDirection - 0.0) > math.EPSILON())
		return(vec3($xDirection, 0.0, $zDirection))
	end
	if (math.abs(vec3.getZ($translation) - vec3.getZ($GLOBAL.path[$GLOBAL.pathIdx])) < 0.1)
		$zDirection = 0.0
	elseif (vec3.getZ($translation) < vec3.getZ($GLOBAL.path[$GLOBAL.pathIdx]))
		$zDirection = 1.0
	elseif (vec3.getZ($translation) > vec3.getZ($GLOBAL.path[$GLOBAL.pathIdx]))
		$zDirection = -1.0
	end
	return(vec3($xDirection, 0.0, $zDirection))
end
