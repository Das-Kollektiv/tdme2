# update engine
function: updateEngine()
	#console.log("-----------------------------")
	#console.log("updateEngine(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()

	# camera
	engine.camera.setLookAt(transform.getTranslation($GLOBAL.transform))
	engine.camera.setLookFrom(transform.getTranslation($GLOBAL.transform) + vec3(0.0, 10.0, 10.0))
	engine.camera.setUpVector(engine.camera.computeUpVector(engine.camera.getLookFrom(), engine.camera.getLookAt())) 

	# update object from physics
	engine.entity.setTransform(logic.getId(), $GLOBAL.transform)

	# update velocity based on arrow keys
	$GLOBAL.velocity = vec3(0.0, 0.0, 0.0)
	if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_LEFT()) == true)
		$GLOBAL.velocity = $GLOBAL.velocity + vec3(-1.0, 0.0, 0.0)
		$GLOBAL.rotation = -90.0
	end
	if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_RIGHT()) == true)
		$GLOBAL.velocity = $GLOBAL.velocity + vec3(1.0, 0.0, 0.0)
		$GLOBAL.rotation = 90.0
	end
	if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_UP()) == true)
		$GLOBAL.velocity = $GLOBAL.velocity + vec3(0.0, 0.0, -1.0)
		$GLOBAL.rotation = 180.0
	end
	if (input.keyboard.isKeyDown(input.keyboard.KEYCODE_DOWN()) == true)
		$GLOBAL.velocity = $GLOBAL.velocity + vec3(0.0, 0.0, 1.0)
		$GLOBAL.rotation = 0.0
	end

	# switch animation
	$currentAnimation = engine.entity.getAnimation(logic.getId())
	if (vec3.computeLengthSquared($GLOBAL.velocity) > 0.01)
		# update velocity
		$GLOBAL.velocity = vec3.normalize($GLOBAL.velocity)
		#	with speed
		$GLOBAL.velocity = $GLOBAL.velocity * $GLOBAL.speed
		#
		if ($currentAnimation != "walk") 
			engine.entity.setAnimation(logic.getId(), "walk")
		end
	else
		if ($currentAnimation != "idle") 
			engine.entity.setAnimation(logic.getId(), "idle")
		end
	end
	
	# mouse
	if (input.mouse.hasMoved() == true)
		$GLOBAL.mouseX = input.mouse.getXUnscaled()
		$GLOBAL.mouseY = input.mouse.getYUnscaled()
	end
	if (input.mouse.isButtonDown(input.mouse.BUTTON_LEFT()) == true)
		if ($GLOBAL.mouseX != -1 && $GLOBAL.mouseY != -1)
			if ($GLOBAL.entityId != "")
				engine.entity.setEffectColorAdd($GLOBAL.entityId, vec4(0.0, 0.0, 0.0, 0.0))
			end
			$GLOBAL.entityId = engine.getEntityIdByMousePosition($GLOBAL.mouseX, $GLOBAL.mouseY)
			if ($GLOBAL.entityId != "")
				engine.entity.setEffectColorAdd($GLOBAL.entityId, vec4(1.0, 0.0, 0.0, 0.0))
			end
			console.log("Clicked@worldposition: ", engine.computeWorldCoordinateByMousePosition($GLOBAL.mouseX, $GLOBAL.mouseY))
		end
	end
end

# update logic
function: updateLogic()
	#console.log("-----------------------------")
	#console.log("updateLogic(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
	# pass velocity to physics object
	world.body.setLinearVelocity(logic.getId(), $GLOBAL.velocity)
	# read back transform from physics object
	$GLOBAL.transform = world.body.getTransform(logic.getId())
	#	apply rotation angle
	transform.setRotationAngle($GLOBAL.transform, 1, $GLOBAL.rotation)
end

# logic added
function: onLogicAdded()
	#console.log("-----------------------------")
	#console.log("onLogicAdded(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
	# initializing
	$GLOBAL.transform = engine.entity.getTransform(logic.getId())
	$GLOBAL.velocity = vec3(0.0, 0.0, 0.0)
	$GLOBAL.speed = 4.0
	$GLOBAL.rotation = 180.0
	$GLOBAL.mouseX = -1
	$GLOBAL.mouseY = -1
	$GLOBAL.entityId = ""
end

# logics processed
function: onLogicsProcessed()
	#console.log("-----------------------------")
	#console.log("onLogicsProcessed(): " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
end

# initialize
on: initialize
	# this is not yet called
	#console.log("-----------------------------")
	#console.log("initialize: " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	#console.log("-----------------------------")
	#console.log("Nothing: " + logic.getId())
	#console.log("-----------------------------")
	#console.log()
end

# an error has occurred
on: error
	console.log("An error occurred")
	script.stop()
end
