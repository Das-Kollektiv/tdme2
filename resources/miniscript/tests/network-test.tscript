# initialize
on: initialize
	console.log("--------------------------")
	console.log("network-test: Initialize")
	console.log("--------------------------")
	console.log()
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	console.log("-----------------------")
	console.log("network-test: Nothing")
	console.log("-----------------------")
	console.log()
	#
	console.log("Fetch drewke.net/tdme2/ via HTTP")
	$result = network.httpclient.get("http://drewke.net/tdme2/")
	if ($result == null)
		console.log("Failed to do HTTP GET request: http://drewke.net/tdme2/")
	else
		console.log("Success: " + ($result.status == $HTTPClient::HTTP_STATUS_OK))
		console.log("HTTP response status: " + $result.status)
		console.log("HTTP headers: " + $result.headers)
		console.log("HTTP response: " + $result.response)
	end
	#
	console.log("Fetch drewke.net/tdme2/ via HTTPS")
	$result = network.httpclient.get("https://drewke.net/tdme2/")
	if ($result == null)
		console.log("Failed to do HTTP GET request: https://drewke.net/tdme2/")
	else
		console.log("Success: " + ($result.status == $HTTPClient::HTTP_STATUS_OK))
		console.log("HTTP response status: " + $result.status)
		console.log("HTTP headers: " + $result.headers)
		console.log("HTTP response: " + $result.response)
	end
	#
	$httpDownloadClient = HTTPDownloadClient()
	$httpDownloadClient->setURL("https://download.freebsd.org/releases/ISO-IMAGES/14.0/FreeBSD-14.0-RELEASE-amd64-bootonly.iso")
	$httpDownloadClient->setFile("FreeBSD-14.0-RELEASE-amd64-bootonly.iso")
	$httpDownloadClient->setHeaders({a: 1})
	console.log("HTTP download client: " + $httpDownloadClient)
	$httpDownloadClient->start()
	console.print("[")
	forCondition($httpDownloadClient->isFinished() == false)
		console.print("-")
		script.wait(1000)
	end
	console.println("]")
	$httpDownloadClient->join()
	console.log("HTTP Download status code: " + $httpDownloadClient->getStatusCode())
	console.log("HTTP Download response headers: " + $httpDownloadClient->getResponseHeaders())
	if (filesystem.removeFile(".", "FreeBSD-14.0-RELEASE-amd64-bootonly.iso") == false)
		console.log("Could not remove file FreeBSD-14.0-RELEASE-amd64-bootonly.iso.")
	end
	#
	script.stop()
end

# an error has occurred
on: error
	console.log("---------------------")
	console.log("network-test: Error")
	console.log("---------------------")
	console.log("An error occurred")
	script.stop()
end
