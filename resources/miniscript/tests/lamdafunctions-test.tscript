# initialize
on: initialize
	console.printLine("---------------------------------")
	console.printLine("lamdafunctions-test: Initialize")
	console.printLine("---------------------------------")
	console.printLine()
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	console.printLine("------------------------------")
	console.printLine("lamdafunctions-test: Nothing")
	console.printLine("------------------------------")
	console.printLine()
	# log array values to console using array.forEach
	console.printLine("array.forEach() log test: [1, 2, 3, 4, 5, 6]")
	$array = [1, 2, 3, 4, 5, 6]
	$array->forEach(($element) -> { console.printLine($element) })
	# log array values to console using array.forRange
	console.printLine("array.forRange(1,3,2) log test: [1, 2, 3, 4, 5, 6]")
	$array->forRange(($element) -> { console.printLine($element) }, 1, 3, 2)
	# sum array values using array.forEach
	$sum = 0
	$array->forEach(($element, &$sum) -> { $sum = $sum + $element }, $sum)
	console.printLine("array.forEach() sum test: [1, 2, 3, 4, 5, 6]: " + $sum)
	# sort array values descending
	console.printLine("array.sort() desc test")
	$array->sort(($a, $b) -> { return($a > $b) })
	# log array values to console using array.forEach
	console.printLine("array.forEach() log test: [6, 5, 4, 3, 2, 1]")
	$array->forEach(($element) -> { console.printLine($element) })
	console.printLine("set.forEach() log test: {aaa, bbb, ccc}")
	# log set keys to console using set.forEach
	$set = {aaa, bbb, ccc}
	$set->forEach(($key) -> { console.printLine($key) })
	console.printLine("set.forEach() log test: {aaa, bbb, ccc}")
	# concatenate set values using set.forEach
	$setConcat = ""
	$set->forEach(($key, &$setConcat) -> 
		{ 
			if ($setConcat->isEmpty() == false) 
				$setConcat = $setConcat + ","
			end
			$setConcat = $setConcat + $key 
		}, 
		$setConcat
	)
	console.printLine("set.forEach() concatenate test: {aaa, bbb, ccc}: " + $setConcat)
	# log map key, value pairs to console using set.forEach
	$map = {a: 1, b: 2, c: 3}
	$map->forEach(($key, $value) -> { console.printLine($key + " = " + $value) })
	# sum map values using map.forEach
	$sum = 0
	$map->forEach(($key, $value, &$sum) -> { $sum = $sum + $value }, $sum)
	console.printLine("map.forEach() sum test: {a: 1, b: 2, c: 3}: " + $sum)
	# done
	script.stop()
end

# an error has occurred
on: error
	console.printLine("----------------------------")
	console.printLine("lamdafunctions-test: Error")
	console.printLine("----------------------------")
	console.printLine("An error occurred")
	script.stop()
end
