# initialize
on: initialize
	console.printLine("-----------------------------")
	console.printLine("filesystem-test: Initialize")
	console.printLine("-----------------------------")
	console.printLine()
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	console.printLine("--------------------------")
	console.printLine("filesystem-test: Nothing")
	console.printLine("--------------------------")
	console.printLine()
	# compose URI
	console.printLine("filesystem.composeURI('filesystem-test', 'test.txt'): " + filesystem.composeURI('filesystem-test', 'test.txt'))
	# canonical URI
	console.printLine("filesystem.getCanonicalURI('filesystem-test/abc/123/../..', 'test.txt'): " + filesystem.getCanonicalURI('filesystem-test/abc/123/../..', 'test.txt'))
	# get filename
	console.printLine("filesystem.getFileName('filesystem-test/abc/123/test.txt'): " + filesystem.getFileName('filesystem-test/abc/123/test.txt'))
	# get pathname
	console.printLine("filesystem.getPathName('filesystem-test/abc/123/test.txt'): " + filesystem.getPathName('filesystem-test/abc/123/test.txt'))
	# remove file extension
	console.printLine("filesystem.removeFileExtension('filesystem-test/abc/123/test.txt'): " + filesystem.removeFileExtension('filesystem-test/abc/123/test.txt'))
	# is path
	console.printLine("filesystem.isPath('.'): " + filesystem.isPath('.'))
	console.printLine("filesystem.isPath('filesystem-test'): " + filesystem.isPath('filesystem-test'))
	# is drive
	console.printLine("filesystem.isDrive('filesystem-test'): " + filesystem.isPath('filesystem-test'))
	console.printLine("filesystem.isDrive('C:'): " + filesystem.isDrive('C:'))
	console.printLine("filesystem.isDrive('C:/'): " + filesystem.isDrive('C:/'))
	# TODO: backslash
	# create folder
	console.printLine("Creating path: filesystem-test")
	if (filesystem.createPath("filesystem-test") == false)
		console.printLine("Could not create path filesystem-test. Exiting.")
		script.stop()
	end
	# get current working dir
	$cwd = filesystem.getCurrentWorkingPathName()
	console.printLine("filesystem.getCurrentWorkingPathName(): " + filesystem.getCurrentWorkingPathName())
	# change path
	console.printLine("Changing path: filesystem-test")
	if (filesystem.changePath("filesystem-test") == false)
		console.printLine("Could not change path filesystem-test. Exiting.")
		script.stop()
	end
	console.printLine("filesystem.getCurrentWorkingPathName(): " + filesystem.getCurrentWorkingPathName())
	# change path
	if (filesystem.changePath($cwd + "/filesystem-test") == false)
		console.printLine("Could not change path filesystem-test. Exiting.")
		script.stop()
	end
	# change path back
	console.printLine("Changing path: " + $cwd)
	if (filesystem.changePath($cwd) == false)
		console.printLine("Could not change path " + $cwd + ". Exiting.")
		script.stop()
	end
	# exists
	console.printLine("filesystem.exists('filesystem-test'): " + filesystem.exists('filesystem-test'))
	console.printLine("filesystem.exists('filesystem-test-123'): " + filesystem.exists('filesystem-test-123')) 
	# list folder
	console.printLine("Listing path: filesystem-test")
	$files = filesystem.list("filesystem-test")
	if ($files == null)
		console.printLine("Could not list path filesystem-test. Exiting.")
		script.stop()
	else
		$i = 0
		forCondition ($i < $files->length())
			console.printLine("File@" + $i + ": " + $files[$i])
			++$i
		end
	end 
	# create binary file
	console.printLine("Creating file: filesystem-test/test.bin")
	$binaryContent = ByteArray()
	$binaryContent->writeInt8(1)
	$binaryContent->writeInt8(2)
	$binaryContent->writeInt8(3)
	$binaryContent->writeInt8(4)
	$binaryContent->writeInt8(5)
	$binaryContent->writeInt8(6)
	if (filesystem.setContent("filesystem-test", "test.bin", $binaryContent) == false)
		console.printLine("Could not create file filesystem-test/test.bin. Exiting.")
		script.stop()
	end
	# binary file size
	console.printLine("Reading file size of filesystem-test/test.bin: " + filesystem.getFileSize("filesystem-test", "test.bin"))
	# reading file
	console.printLine("Reading file: filesystem-test/test.bin")
	$fileContent = filesystem.getContent("filesystem-test", "test.bin");
	if ($fileContent == null)
		console.printLine("Could not read file filesystem-test/test.bin. Exiting.")
		script.stop()
	else
		console.printLine("filesystem-test/test.bin: " + $fileContent)
	end
	# remove binary file
	console.printLine("Removing file: filesystem-test/test.bin")
	if (filesystem.removeFile("filesystem-test", "test.bin") == false)
		console.printLine("Could not remove file filesystem-test/test.bin. Exiting.")
		script.stop()
	end
	# create text file
	console.printLine("Creating file: filesystem-test/test.txt")
	if (filesystem.setContentFromString("filesystem-test", "test.txt", "Arrr. I am a mighty pirate.") == false)
		console.printLine("Could not create file filesystem-test/test.txt. Exiting.")
		script.stop()
	end
	# text file size
	console.printLine("Reading file size of filesystem-test/test.txt: " + filesystem.getFileSize("filesystem-test", "test.txt"))
	# reading file
	console.printLine("Reading file: filesystem-test/test.txt")
	$fileContent = filesystem.getContentAsString("filesystem-test", "test.txt");
	if ($fileContent == null)
		console.printLine("Could not read file filesystem-test/test.txt. Exiting.")
		script.stop()
	else
		console.printLine("filesystem-test/test.txt: " + $fileContent)
	end
	# remove text file
	console.printLine("Removing file: filesystem-test/test.txt")
	if (filesystem.removeFile("filesystem-test", "test.txt") == false)
		console.printLine("Could not remove file filesystem-test/test.txt. Exiting.")
		script.stop()
	end
	# create lines based text file
	console.printLine("Creating file: filesystem-test/test.txt")
	if (filesystem.setContentFromStringArray("filesystem-test", "test.txt", ["Line 1", "Line 2", "Line 3"]) == false)
		console.printLine("Could not create file filesystem-test/test.txt. Exiting.")
		script.stop()
	end
	# lines based file text size
	console.printLine("Reading file size of filesystem-test/test.txt: " + filesystem.getFileSize("filesystem-test", "test.txt"))
	# reading file
	console.printLine("Reading file: filesystem-test/test.txt")
	$fileContent = filesystem.getContentAsStringArray("filesystem-test", "test.txt");
	if ($fileContent == null)
		console.printLine("Could not read file filesystem-test/test.txt. Exiting.")
		script.stop()
	else
		console.printLine("filesystem-test/test.txt: " + $fileContent)
	end
	# rename file and move: they work internally the same
	if (filesystem.rename("filesystem-test/test.txt", "filesystem-test/test-renamed.txt") == false)
		console.printLine("Could not create rename filesystem-test/test.txt to filesystem-test/test-renamed.txt. Exiting.")
	end 
	# list folder
	console.printLine("Listing path: filesystem-test")
	$files = filesystem.list("filesystem-test")
	if ($files == null)
		console.printLine("Could not list path filesystem-test. Exiting.")
		script.stop()
	else
		$i = 0
		forCondition ($i < $files->length())
			console.printLine("File@" + $i + ": " + $files[$i])
			++$i
		end
	end 
	# remove file
	console.printLine("Removing file: filesystem-test/test-renamed.txt")
	if (filesystem.removeFile("filesystem-test", "test-renamed.txt") == false)
		console.printLine("Could not remove file filesystem-test/test-renamed.txt. Exiting.")
		script.stop()
	end
	# remove folder
	console.printLine("Removing path: filesystem-test")
	if (filesystem.removePath("filesystem-test", false) == false)
		console.printLine("Could not remove  path filesystem-test. Exiting.")
		script.stop()
	end
	#
	script.stop()
end

# an error has occurred
on: error
	console.printLine("------------------------")
	console.printLine("filesystem-test: Error")
	console.printLine("-----------------------")
	console.printLine("An error occurred")
	script.stop()
end